__author__ = "Andrei Guliaev"
__copyright__ = "Copyright 2025, Andrei Guliaev"
__email__ = "andrei.guliaev@scilifelab.uu.se"
__license__ = "GPL-3"


# Include pipeline specific rules
include: "rules/common.smk"


# 'All' rule, must be specified before any other modules are
# included, since they also contain 'All' rule
rule all:
    input:
        compile_output_file_list,


# mark duplicates in CCS reads
module prealignment:
    snakefile:
        github(
            "hydra-genetics/prealignment",
            path="workflow/Snakefile",
            branch="develop",
        )
    config:
        config


use rule pbmarkdup from prealignment as mark_duplicates


# map and index BAM files
module alignment:
    snakefile:
        github(
            "hydra-genetics/alignment",
            path="workflow/Snakefile",
            branch="develop",
        )
    config:
        config


use rule pbmm2_index from alignment as pbmm2_index


use rule pbmm2_align from alignment as pbmm2_align


# this module uses my branch
# with pbsv and HiFiCNV
module cnv_sv:
    snakefile:
        github(
            "hydra-genetics/cnv_sv",
            path="workflow/Snakefile",
            branch="develop",
        )
    config:
        config


# bgzip PBSV VCF
# required for filtering
use rule bgzip from cnv_sv as bgzip with:
    input:
        vcf="{file}_T.vcf",
    output:
        "{file}_T.vcf.gz",


# index PBSV VCF
# required for filtering
use rule tabix from cnv_sv as index_pbsv with:
    wildcard_constraints:
        file="^cnv_sv/.+",


# index gzipped DeepSomatic VCF
# required for filtering
use rule tabix from cnv_sv as index_deepsomatic with:
    wildcard_constraints:
        file="^snv_indels/.+",


# run PBSV & HiFiCNV analysis
use rule pbsv_discover from cnv_sv as pbsv_discover


use rule pbsv_call from cnv_sv as pbsv_call


use rule hificnv from cnv_sv as hificnv


# run Sniffles2
use rule sniffles2_call from cnv_sv as sniffles2


# run cnvkit
use rule cnvkit_batch from cnv_sv as cnvkit_batch


use rule cnvkit_diagram from cnv_sv as cnvkit_diagram


# filter VCF files
module filtering:
    snakefile:
        github(
            "hydra-genetics/filtering",
            path="workflow/Snakefile",
            tag="v1.1.0",
        )
    config:
        config


# filter DeepSomatic VCF
use rule bcftools_filter_include_region from filtering as filter_deepsomatic with:
    wildcard_constraints:
        file="^snv_indels/.+",


# use branch with my function
# for flexible BAM input
module snv_indels:
    snakefile:
        github(
            "hydra-genetics/snv_indels",
            path="workflow/Snakefile",
            branch="deepsomatic_flexible_input",
        )
    # snakefile: config["local_snakefile"]["snv_indels"]
    config:
        config


# run DeepSomatic for variant calling
use rule deepsomatic_t_only from snv_indels as deepsomatic_t_only with:
    input:
        bam="data/bam_haplotagged/{sample}_T.haplotagged.bam.bai",
        bai="data/bam_haplotagged/{sample}_T.haplotagged.bam.bai",
        ref=config.get("reference", {}).get("fasta", ""),
        bed=config.get("reference", {}).get("design_bed", ""),
        pon=config.get("reference", {}).get("pon", ""),
    output:
        tmpdir=temp(directory("snv_indels/deepsomatic_t_only/{sample}.tmp")),
        vcf="snv_indels/deepsomatic_t_only/{sample}_T.vcf.gz",
