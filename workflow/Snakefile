__author__ = "Andrei Guliaev"
__copyright__ = "Copyright 2025, Andrei Guliaev"
__email__ = "andrei.guliaev@scilifelab.uu.se"
__license__ = "GPL-3"


# Include pipeline specific rules
include: "rules/common.smk"


# 'All' rule, must be specified before any other modules are
# included, since they also contain 'All' rule
rule all:
    input:
        compile_output_file_list,


# Include pipeline specific rules
module alignment:
    snakefile:
        github(
            "hydra-genetics/alignment",
            path="workflow/Snakefile",
            branch="develop",
        )
    config:
        config


use rule * from alignment as alignment_*


module annotation:
    snakefile:
        github(
            "hydra-genetics/annotation",
            path="workflow/Snakefile",
            tag="v1.1.0",
        )
    config:
        config


use rule tabix_vcf from annotation


module snv_indels:
    snakefile:
        github(
            "hydra-genetics/snv_indels",
            path="workflow/Snakefile",
            branch="develop",
        )
    config:
        config


use rule deepsomatic_tn from snv_indels as deepsomatic_tn


module filtering:
    snakefile:
        github(
            "hydra-genetics/filtering",
            path="workflow/Snakefile",
            tag="v1.1.0",
        )
    config:
        config


use rule bcftools_filter_include_region from filtering as deepsomatic_filtering with:
    wildcard_constraints:
        file="^snv_indels/.+",

# this module is not ready yet
# the local version contains pbsv and HiFiCNV
module cnv_sv:
    snakefile:
        config["local_snakefile"]["cnv_sv"]
    config:
        config


use rule pbsv_call, pbsv_discover, hificnv from cnv_sv
