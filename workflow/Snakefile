__author__ = "Andrei Guliaev"
__copyright__ = "Copyright 2025, Andrei Guliaev"
__email__ = "andrei.guliaev@scilifelab.uu.se"
__license__ = "GPL-3"


# Include pipeline specific rules
include: "rules/common.smk"


# 'All' rule, must be specified before any other modules are
# included, since they also contain 'All' rule
rule all:
    input:
        compile_output_file_list,


# this module uses my branch
# with pbsv and HiFiCNV
module cnv_sv:
    snakefile:
        github(
            "hydra-genetics/cnv_sv",
            path="workflow/Snakefile",
            branch="hificnv-pbsv",
        )
    config:
        config

# bgzip PBSV VCF
# required for filtering
use rule bgzip from cnv_sv with:
    input: vcf="{file}.vcf"
    output: "{file}.vcf.gz"

# index PBSV VCF
# required for filtering
use rule tabix from cnv_sv as index_pbsv with:
    wildcard_constraints:
        file="^cnv_sv/.+",

# index gzipped DeepSomatic VCF
# required for filtering
use rule tabix from cnv_sv as index_deepsomatic with:
    wildcard_constraints:
        file="^snv_indels/.+",

# run PBSV analysis
use rule pbsv_call, pbsv_discover from cnv_sv


# filter VCF files
module filtering:
    snakefile:
        github(
            "hydra-genetics/filtering",
            path="workflow/Snakefile",
            tag="v1.1.0",
        )
    config:
        config

# filter DeepSomatic VCF
use rule bcftools_filter_include_region from filtering as filter_deepsomatic with:
    wildcard_constraints:
        file="^snv_indels/.+",

# filter PBSV VCF
use rule bcftools_filter_include_region from filtering as filter_pbsv with:
    wildcard_constraints:
        file="^cnv_sv/.+",

# use branch with my function
# for flexible BAM input 
module snv_indels:
    snakefile:
        github(
            "hydra-genetics/snv_indels",
            path="workflow/Snakefile",
            branch="deepsomatic_flexible_input",
        )
    config:
        config

# run DeepSomatic for variant calling
use rule deepsomatic_tn from snv_indels as deepsomatic_tn

# run HiFiCNV for copy number analysis
use rule hificnv from cnv_sv as hificnv